import Mathlib.Tactic
import Mathlib.Algebra.NeZero
import Mathlib.Logic.IsEmpty
import Mathlib.Data.Real.EReal
import Mathlib.Algebra.Field.Basic
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.LinearAlgebra.TensorProduct.Prod
import Mathlib.Algebra.Module.Equiv
import Mathlib.Topology.Defs.Basic
import Mathlib.Topology.Basic
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Data.Set.Intervals.Basic
import Mathlib.Algebra.Module.Submodule.Pointwise
import Mathlib.Data.Fin.VecNotation
import Axler.Chapter1.MyComplex
import Axler.Chapter1.Complex
import Axler.Chapter1.Rn
import Axler.Chapter1.Vc

/-!

# Chapter 1

-/

/-
## Section 1A: Complex and `R^n`

### Complex (1.1 and 1.2)

`Complex` is built into Mathlib, but it's instructive to build a version of complex numbers
and prove the basic laws in Lean4. We do this in `Axler.Chapter1.MyComplex` (with a little more work,
we could prove that the `Complex` numbers form a field -- this is the (https://github.com/ImperialCollegeLondon/complex-number-game)[Complex Number Game]
which with a little work is instructive to port to Lean4)

In `Axler.Chapter1.Complex` we prove the same laws using the built-in implementations.

### `R^n` and `F^n`

`R^n` in Lean4 is `Fin n â†’ R`. This might be confusing if you think about a vector in, say,
`R^3` as a vector `[x, y z]`, but if we think of that vector instead as a specific vector that
maps `0, 1, 2` to `x`, `y`, `z`, then `Fin 3 â†’ â„` is the set of all such mappings.

We can get a specific vector via the built-in notaton `![]`. For example, if we previously have
defined `x`, `y`, and `z` as reals, `![x, y z]` gives us a function of the type `Fin 3 â†’ â„`
-/
variable (x y z: â„)
#check ![x, y, z]
/-

In Mathlib, the laws for vector addition are encapsulated in `AddCommGroup`, and we get a built-in
lawful implementation of vector addition in Mathlib via definitions in the `Pi` namespace (`Pi` here
refers to Î -types, or the types of dependent functions, of which `Fin 3 â†’ â„` is an instance). Again,
it can be instructive to build an implementation of these definitions and prove that they implement the
laws for `AddCommGroup`, which is done in `Axler.Chapter1.Rn`. Again, though, we get the automagic
version for free:
-/
#synth (n: â„•) â†’ AddCommGroup (Fin n â†’ â„)

/-
We can extend these defintions and proofs to all fields via a bit more work in `Axler.Chapter1.Fn`. Again, we
need not do this manually, because this is built-in via Mathlib:
-/
variable {F: Type _}
variable [Field F]
#synth (n: â„•) -> AddCommGroup (Fin n â†’ F)

/-
## Exercises 1A

Exercises 1-6, 11, 13, and 14 are proved in `Axler.Chapter1.Fn`
-/

/- Exercise 9-/
-- example : âˆƒx, ![(4 : â„) , -3, 1, 7] + (2 * x) = ![5, 9, -6, 8] := by
--   use ![1/2, 6, -7/2, 1/2]
--   sorry

/- Exercise 12-/
example {n: â„•} {a: Fin n â†’ F } {x y: F}: x â€¢ (y â€¢ a) = (x * y) â€¢ a := by rw [smul_smul]
/- Exercise 15-/
example {n: â„•} {a: Fin n â†’F} {x y : F}: (x + y) â€¢ a = x â€¢ a + y â€¢ a := by rw [add_smul]

/-
## Section 1B: Vector Spaces
-/

/-

### 1.22: `F^n` is a vector space over `ğ”½`

In Mathlib, vector spaces are `Module`s over `Field`s (algebraically, modules are generalizations of
vector spaces to rings); so what we want is to show that the `Module` laws can be proven for a
suitable implementation of `F^n`

We prove this in `Axler.Chapter1.Fn` and this is built in to Mathlib:
-/

#synth (n: â„•) â†’ Module F (Fin n â†’ F)

/-

### 1.23: `F^âˆ` is a vector space over `F`

Here our vectors are functions that map each value of `â„•` to a real, or `f: â„• â†’ â„` and therefore we
can prove the desired property with a one liner (of course, we could also trivially modify `Axler.Chapter1.Fn`
so that our `term` is defined as  `â„• â†’ â„` and all the proofs would be identical)
-/

#synth Module F (â„• â†’ F)

/-
### 1.25: `F^ğ‘†` is a vector space

In Mathlib, we are already ahead here, because we've thought about vectors as functions.

Again, this is built-in to the `Pi` representation, and we could easily modify `Axler.Chapter1.Fn` in terms
of general types, rather than the specific type `Fin n`

In Lean4 and Mathlib, types are more general than `Set`, so we can prove this over an arbitrary type or
a Mathlib `Set` proper.
-/

variable {Î± : Type*}
#synth Module F (Î± â†’ F)
#synth Module F (Set Î± â†’ F)

/-
From now on we are generally going to be dealing with arbitrary vectors that conform to the
`AddCommGroup` axioms that form a vector space over field `F`: `Module F V` so we set up our
variables accordingly.
-/

variable [Field F] [AddCommGroup V] [Module F V]

/-
### 1.26 uniqueness of the additive identity
-/


example {a b: V}: a + b = a â†” b = 0 := add_right_eq_self

/-
### 1.27: uniqueness of the additive inverse
-/

example {a b c: V}: a + b = 0 â†’ a + c = 0 â†’ b = c := fun h1 => fun h2 => neg_unique h1 h2

/-
### 1.30: the number 0 times a vector

This is `zero_smul` in the `Module` laws
-/

/-
### 1:31: a number times the vector 0

-/

example {a: V} {x: F}: a = 0 â†’ x â€¢ a = 0 := fun h => smul_eq_zero_of_right x h

/-
### 1.32: the number âˆ’1 times a vector
-/

example {a: V}: (-1: F) â€¢ a = -a := neg_one_smul F a

/-
## Exercises 1B
-/

/-
### Exercise 1
-/

example {a: V}: -(-a) = a := neg_neg a

/-
### Exercise 2
Suppose ğ‘ âˆˆ ğ…,ğ‘£ âˆˆ ğ‘‰,and ğ‘ğ‘£ = 0.Prove that ğ‘ = 0 or ğ‘£ = 0
-/

example {v: V} {a: F}: a â€¢ v = 0 â†” a = 0 âˆ¨ v = 0 := smul_eq_zero

/-
### Exercise 3

Suppose ğ‘£, ğ‘¤ âˆˆ ğ‘‰. Explain why there exists a unique ğ‘¥ âˆˆ ğ‘‰ such that ğ‘£ + 3ğ‘¥ = ğ‘¤.
-/
variable [CharZero F] -- make sense of 3, etc.

example {v w: V}: âˆƒx, v + (3: F )â€¢x = w := by
  use ((3â»Â¹: F) â€¢ (w - v))
  rw [smul_smul, mul_inv_cancel, one_smul, add_comm, sub_add_cancel]
  exact three_ne_zero

-- a more general proof
example {v w: V} {a: F}: a â‰  0 â†’ âˆƒx, v + aâ€¢x = w := fun h =>
  âŸ¨aâ»Â¹ â€¢ (w - v), by
    rw [smul_smul, mul_inv_cancel, one_smul, add_comm, sub_add_cancel]; assumptionâŸ©

/-
### Exercise 4

The empty set is not a vector space. The empty set fails to satisfy only one of the requirements
listed in the definition of a vector space (1.20). Which one?

In Mathlib, it's easier to do this backwards and prove that an arbitrary vector space is not empty.

This is witnessed through the `zero` element of the vector space via `Zero.nonempty`
-/

example: Â¬IsEmpty V := not_isEmpty_of_nonempty V


/-
### Exercise 5

Show that in the definition of a vector space (1.20), the additive inverse condition can be replaced with the condition that
0ğ‘£ = 0 for all ğ‘£ âˆˆ ğ‘‰.
Here the 0 on the left side is the number 0, and the 0 on the right side is the
additive identity of ğ‘‰.

Here we want to show that `v - v = 1â€¢v + -1â€¢ v = (1 + -1) â€¢ v = 0 â€¢ v` so we can use our updated axiom
-/

example: âˆ€(v: V), (0: F)â€¢v = 0 â†’ âˆƒw, v + w = 0 := fun v h => by
  use ((-1: F) â€¢ v)
  rw [â†one_smul F v, smul_comm, one_smul F ((-1: F) â€¢ v), â†add_smul, add_right_neg]; assumption

/-
### Exercise 6

Let âˆ and âˆ’âˆ denote two distinct objects, neither of which is in ğ‘.
Define an addition and scalar multiplication on ğ‘ âˆª {âˆ, âˆ’âˆ} as you could guess from the notation. Specifically, the sum and product of two real numbers is as usual, and for ğ‘¡ âˆˆ ğ‘ define
ğ‘¡*âˆ =
* t < 0: âˆ’âˆ
* t = 0: 0
* t > 0: âˆ

ğ‘¡*-âˆ =
* t < 0: âˆ
* t = 0: 0
* t > 0: -âˆ

ğ‘¡ + âˆ = âˆ + ğ‘¡ = âˆ + âˆ = âˆ,
ğ‘¡+(âˆ’âˆ) = (âˆ’âˆ)+ğ‘¡ = (âˆ’âˆ)+(âˆ’âˆ) = âˆ’âˆ,
âˆ+(âˆ’âˆ) = (âˆ’âˆ)+âˆ = 0.

With these operations of addition and scalar multiplication, is ğ‘ âˆª {âˆ, âˆ’âˆ}
a vector space over ğ‘? Explain.

First, we note that the defition proposed is exactly that of the `Mathlib.Data.Real.EReal`
implementation of extended reals.

We also note there's no automagic here:
--#synth Module EReal EReal
-- Errors "failed to synthesize instance Semiring EReal"

The issue is that multiplcation does not commute, so we can't get a defintion
of multiplcation by either `â„` or `EReal` that follows the laws.

We want to get a proof that commutation is violated: `Â¬âˆ€ (x y z: EReal), x * z + y * z = (x + y) * z`
-/


-- `1*âŠ¥ + -1*âŠ¥ = âŠ¥`
lemma EReal.one_mul_add:  1*âŠ¥ + (-1: EReal )*âŠ¥ = (âŠ¥: EReal) := by
  have h: (âŠ¥: EReal) = âŠ¥ + âŠ¤ := rfl
  have h2: (âŠ¥: EReal) + âŠ¤ = (1: EReal) *âŠ¥ + (-1: EReal)*âŠ¥ := by
    have h3 : (âŠ¥ : EReal) = 1*âŠ¥ := Linarith.without_one_mul rfl
    have h4: (âŠ¤: EReal) = -âŠ¥ := rfl
    have h5: (-âŠ¥ : EReal) = -1 * âŠ¥ := neg_eq_neg_one_mul âŠ¥
    rw [h3, h4, h5]
    simp
  rw [h, h2]
  simp

-- but `(1 + -1)*âŠ¥ = 0`
lemma EReal.one_add_mul: ((1: EReal) + (-1: EReal))*âŠ¥ = 0 := by
  have h: (1: EReal) + -1 = 1 - 1 := rfl
  have h2: (1: EReal) - 1 = 0 := by rw [â†EReal.coe_one, â†EReal.coe_sub, sub_self, EReal.coe_zero]
  rw [h, h2, zero_mul]

lemma EReal.one_mul_add_ne_one_add_mul: 1*âŠ¥ + (-1: EReal )*âŠ¥ â‰  ((1: EReal) + -1)*âŠ¥ :=  by
  rw [EReal.one_mul_add, EReal.one_add_mul]
  exact bot_ne_zero

theorem not_all_add_mul: Â¬âˆ€ (x y z: EReal), x * z + y * z = (x + y) * z := fun h =>
  let h2  :=  EReal.one_mul_add_ne_one_add_mul
  h2 (h 1 (-1: EReal) âŠ¥)

/-
### Exercise 7

Suppose `ğ‘†` is a nonempty set. Let `ğ‘‰_ğ‘†` denote the set of functions from `ğ‘†` to `ğ‘‰`.
Define a natural addition and scalar multiplication on `ğ‘‰_ğ‘†`, and show that `ğ‘‰_ğ‘†` is a vector space with these definitions.

We can perform the addition pointwise, since the underlying vectors add properly, and use the field
over which the vector space is defined for scalar multiplication, and again this is automagic in Mathlib.
-/

#synth Module F (Î± â†’ V) -- note again that types are more general than `Set` in Lean and `Mathlib`


/-
### Exercise 8

Suppose ğ‘‰ is a real vector space.
* The complexification of `ğ‘‰`, denoted by `ğ‘‰^ğ‚` , equals `ğ‘‰ Ã— ğ‘‰`. An element of
`ğ‘‰^ğ‚` is an ordered pair `(ğ‘¢,ğ‘£)`,where `ğ‘¢,ğ‘£ âˆˆ ğ‘‰`,but we write this as `ğ‘¢ + ğ‘–ğ‘£`
* Addition on `ğ‘‰^ğ‚` is defined by
```âˆ€ ğ‘¢_1,ğ‘£_1,ğ‘¢_2,ğ‘£_2 âˆˆ ğ‘‰, (ğ‘¢_1 +ğ‘–ğ‘£_1) + (ğ‘¢_2 +ğ‘–ğ‘£_2) = (ğ‘¢_1 +ğ‘¢_2) + ğ‘–(ğ‘£_1 +ğ‘£_2) ```.
* Complex scalar multiplication on `ğ‘‰^ğ‚` is defined by `(ğ‘ + ğ‘ğ‘–)(ğ‘¢ + ğ‘–ğ‘£) = (ğ‘ğ‘¢ âˆ’ ğ‘ğ‘£) + ğ‘–(ğ‘ğ‘£ + ğ‘ğ‘¢)`
for all `ğ‘, ğ‘ âˆˆ ğ‘` and all `ğ‘¢, ğ‘£ âˆˆ ğ‘‰`.

Prove that with the definitions of addition and scalar multiplication as above,
`ğ‘‰^ğ‚` is a complex vector space.
Think of ğ‘‰ as a subset of `ğ‘‰^ğ‚` by identifying `ğ‘¢ âˆˆ ğ‘‰` with `ğ‘¢ + ğ‘–0`. The construction of `ğ‘‰^ğ‚` from `ğ‘‰`
can then be thought of as generalizing the construction of `â„‚^ğ‘›` from `â„^ğ‘›`.

We do this two different ways. The first, in keeping with the level of proof tools we have in LADR right now,
takes the construction of `V^C` literally, and shows that as defined, `V^C` is a vector space with scalar multiplication
by `â„‚`.

This basic appraoch is in `Axler.Chapter1.Vc`

The second approach uses more of the power of `Mathlib` but requires more mathmatics than is currently presented in LADR.
(It uses linear maps, which are the subject of Chapter 3, and tensor products, which are in 9A)

Here, we treat the complexification of a real vector space as the tensor product of the vector space with `â„‚`. Again,
proof this is a complex vector space is built in to `Mathlib`.

We'd then like to show that we aren't cheating by showing that the tensor product is, indeed, equivalent
to the product of our vector space. To show equivalence of two types in `Mathlib` in general we need to make an
`Equiv` that provides functions that map each way (to and from), then show that the maps are inverses of one another.
Here we prove a stronger equivalence,`LinearEquiv`, which adds an additional constraint that addition and
scalar multiplication are preserved across the equivalence.
-/

variable (V) [AddCommGroup V] [Module â„ V]

open scoped TensorProduct

#synth Module â„‚ (â„‚ âŠ—[â„] V)

/--
Trivial linear map betwen `V Ã— V` and `â„‚ âŠ—[â„] V` that treats the first part as real and the second as imaginary
-/
noncomputable def toFun: V Ã— V â†’â‚—[â„] â„‚ âŠ—[â„] V where
  toFun p := match p with
  |  âŸ¨fst, sndâŸ© => ((1: â„‚) âŠ—â‚œ[â„] fst) + Complex.I âŠ—â‚œ[â„] snd
  map_add' p1 p2 := by dsimp; simp [TensorProduct.tmul_add]; abel_nf
  map_smul' r p := by dsimp; simp [TensorProduct.tmul_smul]

/--
Trivial inverted map, that contructs a `Prod` by scaling a vector by the real and imaginary parts of a complex number
-/
noncomputable def invFun: â„‚ âŠ—[â„] V â†’â‚—[â„] V Ã— V :=
  TensorProduct.lift <| LinearMap.mkâ‚‚
  â„ (fun z v â†¦ (z.re â€¢ v, z.im â€¢ v))
    (by intros; ext <;> dsimp <;> rw [add_smul])
    (by intros; ext <;> dsimp <;> simp [mul_smul])
    (by intros; ext <;> dsimp <;> simp [smul_add])
    (by intros; ext <;> dsimp <;> rw [smul_comm])

theorem toFun_invFun_comp_eq_id: LinearMap.comp (@toFun V _ _) (@invFun V _ _) = LinearMap.id := by
  simp only [toFun, invFun]; ext z v; dsimp; simp [TensorProduct.tmul_add, TensorProduct.smul_tmul]
  rw [TensorProduct.smul_tmul', TensorProduct.smul_tmul', â†TensorProduct.add_tmul (z.re â€¢ 1) (z.im â€¢ Complex.I) v]
  simp only [Complex.real_smul, mul_one, Complex.re_add_im]

theorem invFun_toFun_comp_eq_id: LinearMap.comp (@invFun V _ _) (@toFun V _ _) = LinearMap.id := by
  simp only [toFun, invFun]; apply LinearMap.ext; intro p; simp

/--
Our proof of equivalence
-/
noncomputable def prod_rLinearEquiv_ComplexTensor: (V Ã— V) â‰ƒâ‚—[â„] (â„‚ âŠ—[â„] V) :=
  LinearEquiv.ofLinear (@toFun V _ _) (@invFun V _ _) (@toFun_invFun_comp_eq_id V _ _) (@invFun_toFun_comp_eq_id V _ _)

/-
## 1C: Subspaces


We have `variable (V) [AddCommGroup V] [Module ğ”½ V]` as the `Mathlib` declaration for a vector space over `ğ”½`

A subspace is a restiction of `V` such that the subspace laws are preserved, which in `Mathlib` is expressed a `Set` over `V`
plus the three laws, wrapped up in a `Submodule` definition -/

/-
### 1.35 example (1):
If `ğ‘ âˆˆ ğ”½`, then `{(ğ‘¥_1,ğ‘¥_2,ğ‘¥_3,ğ‘¥_4) âˆˆ ğ”½^4 âˆ¶ ğ‘¥_3 = 5*ğ‘¥_4 + ğ‘}` is a subspace of ğ…^4 if and only if `ğ‘ = 0`

We first show that `0 âˆˆ {(ğ‘¥_1,ğ‘¥_2,ğ‘¥_3,ğ‘¥_4) âˆˆ ğ”½^4 âˆ¶ ğ‘¥_3 = 5*ğ‘¥_4 + ğ‘}` if and only if `b = 0`, then, setting
`b = 0`, that the resulting `Set` is a `Submodule`
-/

theorem ex_1_iff_eq_zero : (b: F) â†’ 0 âˆˆ {f: Fin 4 â†’ F | 5*(f 2) = (f 4) + b} â†” b = 0 := by
  intro b; simp only [Set.mem_setOf_eq, Pi.zero_apply, mul_zero, zero_add]
  constructor <;> intro h <;> exact h.symm

def ex1_Subspoace: Submodule F (Fin 4 â†’ F) where
  carrier := {f: Fin 4 â†’ F | 5*(f 2) = (f 4)}
  zero_mem' := by simp only [Set.mem_setOf_eq, Pi.zero_apply, mul_zero]
  add_mem' := by simp only [Set.mem_setOf_eq, Pi.add_apply]; intro a b h1 h2; rw [mul_add, h1, h2]
  smul_mem' := by
    simp only [Set.mem_setOf_eq, Pi.smul_apply, smul_eq_mul]
    intro c f h1
    have h2: 5 * (c * f 2) = c * (5 * f 2) := by ring_nf
    rw [h2, h1]

/-
### 1.35 example (2)

The set of continuous real-valued functions on the interval `[0,1]` is a subspace
of `ğ‘^{[0,1]}`

This example, and the next two, require additional constructs from `Mathlib`

(mostly taken from https://github.com/martincmartin/linear_algebra_done_right/)
-/

def ex2_Subspace: Submodule â„ ( Set.Icc (0 : â„) (1 : â„) â†’ â„) where
  carrier := {f | Continuous f}
  zero_mem' := by simp only [Set.mem_setOf_eq]; exact continuous_zero
  add_mem' := by simp only [Set.mem_setOf_eq]; intro a b h1 h2; exact h1.add h2
  smul_mem' := by simp only [Set.mem_setOf_eq]; intro a f h; exact continuous_const.mul h

/-
### 1.35 example (3)

The set of differentiable real-valued functions on `â„` is a subspace of `â„^â„`
(mostly taken from https://github.com/martincmartin/linear_algebra_done_right/)
-/

def ex3_Subspace: Submodule â„ (â„ â†’ â„ ) where
  carrier := {f | Differentiable â„ f}
  zero_mem' := differentiable_const _
  add_mem' :=  Differentiable.add
  smul_mem' c _ := (differentiable_const c).smul

/-
### 1.35 example (4)

The set of differentiable real-valued functions `ğ‘“` on the interval `(0, 3)` such that `ğ‘“â€²(2) = ğ‘` is a
subspaceof `â„^{(0,3)}` if and only if `ğ‘ = 0`.

Just like ex 1 above, the issue here is with the existence of `0` -- our `0` function is the constant
function that sends all values to `0`, which implies that `f'(x) = 0` for all `x`.

(portions borrowed from https://github.com/martincmartin/linear_algebra_done_right/)

-/

theorem ex4_iff_eq_zero : (b: â„) â†’ 0 âˆˆ {f | (âˆ€ x âˆˆ Set.Ioo (0 : â„)  (3: â„), DifferentiableAt â„ f x) âˆ§ (HasDerivAt f (b: â„)  (2: â„))} â†” b = 0 := by
  intro b
  constructor <;> intro h
  . have h2:= hasDerivAt_const (2:â„) (0: â„)
    rw [â†@Pi.zero_def] at h2
    exact HasDerivAt.unique h.right h2
  . constructor
    . intro _ _
      exact differentiableAt_const 0
    . rw [h]
      apply hasDerivAtFilter_const

def ex4_Subspace: Submodule â„ (â„ â†’ â„) where
  carrier := { f | (âˆ€ x âˆˆ Set.Ioo (0 : â„)  (3: â„), DifferentiableAt â„ f x) âˆ§ (HasDerivAt f 0 2)}
  zero_mem' := by
    constructor
    . intro _ _
      exact differentiableAt_const 0
    . apply hasDerivAtFilter_const
  add_mem' := by
    intro a b h1 h2
    constructor
    . intro x h
      rcases (h1.left x h) with âŸ¨ f'x, f_has âŸ©
      rcases (h2.left x h) with âŸ¨ g'x, g_has âŸ©
      use f'x + g'x
      exact f_has.add g_has
    . have h3:= h1.right.add h2.right
      simp [@Pi.add_def] at *
      assumption
  smul_mem' := by
    intro a f hf
    cases' hf with hf1 hf2
    constructor
    . intro b h
      have h2 := (hf1 b h).smul_const a
      simp [@Pi.smul_def] at *
      simp_rw [mul_comm a (f _)]
      exact h2
    . have h := hf2.smul_const a
      simp [@Pi.smul_def] at *
      simp_rw [mul_comm a (f _)]
      exact h

/-
### 1.35 Example 5

The set of all sequences of complex numbers with limit `0` is a subspace of `â„‚^{âˆ}`

(mostly taken from https://github.com/martincmartin/linear_algebra_done_right/)

-/

open Filter Topology

def ex5_Subspace : Subspace â„‚ (â„• â†’ â„‚) where
  carrier := {u | Tendsto u Filter.atTop (ğ“ 0)}
  add_mem' := by
    simp
    intro u v hu hv
    have h := hu.add hv
    simp [Pi.add_def] at *
    assumption
  zero_mem' := tendsto_const_nhds
  smul_mem' := by
    simp
    intro c u hu
    have := hu.const_mul c
    simp [Pi.add_def] at *
    assumption

/-
### Sums of Subspaces
-/

/-
#### Example 1.37

Suppose `ğ‘ˆ` is the set of all elements of `ğ”½^3` whose second and third coordinates equal 0, and `ğ‘Š` is the set of all elements of `ğ…^3` whose first and third coordinates equal 0:
`ğ‘ˆ={(ğ‘¥,0,0) âˆˆ ğ…^3 âˆ¶ğ‘¥ âˆˆ ğ…}` and `ğ‘Š={(0,ğ‘¦,0) âˆˆ ğ…^3 âˆ¶ğ‘¦ âˆˆ ğ…}`.
Then as you should verify.

`ğ‘ˆ+ğ‘Š={(ğ‘¥,ğ‘¦,0) âˆˆ ğ…^3 âˆ¶ğ‘¥,ğ‘¦ âˆˆ ğ…}`

We first prove that `U` and `W` are subspaces, then that `U + W` have the form provided, then that `U + W` are subspaces.

-/
open Pointwise

def subspace_ex1_37_U: Submodule â„ (Fin 3 â†’ â„) where
  carrier :=  { ![xâ‚, 0, 0] | (xâ‚: â„)}
  zero_mem' := by simp
  add_mem' := by aesop
  smul_mem' := by simp

def subspace_ex1_37_V: Submodule â„ (Fin 3 â†’ â„) where
  carrier := { ![0, xâ‚‚, 0] |  (xâ‚‚ : â„)}
  zero_mem' := by simp
  smul_mem' := by simp
  add_mem' := by aesop

theorem ex1_37: { ![xâ‚, 0, 0] | (xâ‚: â„)} + { ![0, xâ‚‚, 0] |  (xâ‚‚ : â„)} = { ![xâ‚, xâ‚‚, 0] | (xâ‚: â„) (xâ‚‚: â„)} := by
  ext x ; simp [Set.mem_add]

def subspace_ex1_37_VU: Submodule â„ (Fin 3 â†’ â„) where
  carrier :=  { ![xâ‚, xâ‚‚, 0] | (xâ‚: â„) (xâ‚‚: â„)}
  zero_mem' := by simp
  smul_mem' := by aesop
  add_mem' := by aesop


#check (subspace_ex1_37_U + subspace_ex1_37_V)

/-
#### Example 1.38

`ğ‘ˆ = {(ğ‘¥,ğ‘¥,ğ‘¦,ğ‘¦) âˆˆ ğ…^4 âˆ¶ğ‘¥,ğ‘¦ âˆˆ ğ…}` and `ğ‘Š={(ğ‘¥,ğ‘¥,ğ‘¥,ğ‘¦) âˆˆ ğ…^4 âˆ¶ğ‘¥,ğ‘¦ âˆˆ ğ…}`

prove

`ğ‘ˆ + ğ‘Š = {(ğ‘¥,ğ‘¥,ğ‘¦,ğ‘§) âˆˆ ğ…^4 âˆ¶ğ‘¥,ğ‘¦,ğ‘§ âˆˆ ğ…}`
-/

def subspace_ex1_38_U: Submodule F (Fin 4 â†’ F) where
  carrier :=  { ![x, x, y, y] | (x: F) (y: F)}
  zero_mem' := by simp
  smul_mem' := by aesop
  add_mem' := by aesop

def subspace_ex1_38_W: Submodule F (Fin 4 â†’ F) where
  carrier :=  { ![x, x, x, y] | (x: F) (y: F)}
  zero_mem' := by simp
  smul_mem' := by aesop
  add_mem' := by aesop

theorem ex1_38: { ![x, x, y, y] | (x: F) (y: F)} + { ![x, x, x, y] | (x: F) (y: F)} = { ![x, x, y, z] | (x: F) (y: F) (z: F)} := by
  ext x ; simp [Set.mem_add]; constructor
  . intro h
    rcases h with âŸ¨a, b, c, d, hâŸ©
    use (a + c), (b + c), (b + d)
  . intro h
    rcases h with âŸ¨a, b, c, hâŸ©
    use a, b, 0, (c - b)
    simp_all

/-

Axler, Linear Algebra Done Right, Example 1.40:

"Suppose `ğ‘‰_1, ..., ğ‘‰_ğ‘š` are subspaces of `ğ‘‰`. Then `ğ‘‰_1 + â‹¯ + ğ‘‰_ğ‘š` is the smallest subspace of `ğ‘‰`
containing `ğ‘‰_1, ..., ğ‘‰_ğ‘š`."

We know `V_1 + ... + V_m` is a subspace because the `Submodule.pointwiseAddCommMonoid` instance
proves additive closure.

If we can prove the claim of "smallest" for pairs of subspaces `U` and `V` we can prove for arbitrary
additve chains.

There's a subtlty here -- Mathlib's version of `+` for `Submodule` is actually `âŠ”` (`Sup`) under the hood, and
and the actual proof relies on very general proof machinery about lattices (through `Submodule`'s inheritence
from `AddSubmonoid` which has a `CompleteLattice` implementation).

In practice, it would be rare to use `Pointwise` and the below theorem would be written
`{Vâ‚ Vâ‚‚ W : Submodule F V} : (âˆ€ x : V, x âˆˆ Vâ‚ âˆ¨ x âˆˆ Vâ‚‚ â†’ x âˆˆ W) â†’ Vâ‚ âŠ” Vâ‚ â‰¤ W`

credit to Patrick Massot for the proof
-/
open Pointwise

variable {F V : Type*} [Field F] [AddCommGroup V] [Module F V]
theorem ex_1_40 {Vâ‚ Vâ‚‚ W : Submodule F V} : (âˆ€ x : V, x âˆˆ Vâ‚ âˆ¨ x âˆˆ Vâ‚‚ â†’ x âˆˆ W) â†’ Vâ‚ + Vâ‚ â‰¤ W := by
  intro h
  simp only [Submodule.add_eq_sup]
  apply sup_le; intro _ _; aesop
