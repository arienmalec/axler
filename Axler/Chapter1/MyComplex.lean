import Mathlib.Data.Real.Basic
import Mathlib.Tactic

/-!

# MyComplex

Axler 1.3: prove the properties of complex arithmetic

First, do this de novo in lean by definng a datatype `MyComplex` and proving from
first principles (next we will show this via the built in `Complex` type):

## Commutativity
ğ›¼ + ğ›½ = ğ›½ + ğ›¼ and ğ›¼ğ›½ = ğ›½ğ›¼ for all ğ›¼, ğ›½ âˆˆ ğ‚
## Associativity
(ğ›¼ + ğ›½) + ğœ† = ğ›¼ + (ğ›½ + ğœ†) and (ğ›¼ğ›½)ğœ† = ğ›¼(ğ›½ğœ†) for all ğ›¼, ğ›½, ğœ† âˆˆ ğ‚

## Identities
ğœ† + 0 = ğœ† and ğœ†1 = ğœ† for all ğœ† âˆˆ ğ‚

## Existence of additive inverse
For every ğ›¼ âˆˆ ğ‚, there exists a unique ğ›½ âˆˆ ğ‚ such that ğ›¼ + ğ›½ = 0

## Existence of multiplicative inverse
For every ğ›¼ âˆˆ ğ‚ with ğ›¼ â‰  0, there exists a unique ğ›½ âˆˆ ğ‚ such that ğ›¼ğ›½ = 1

## Distributive property
ğœ†(ğ›¼ + ğ›½) = ğœ†ğ›¼ + ğœ†ğ›½ for all ğœ†, ğ›¼, ğ›½ âˆˆ ğ‚.

-/

/-- Representation of a complex number with `Real`-valued real and imaginary parts
-/
@[ext]
structure MyComplex where
  mk ::
    /-- The real part-/
    (re: Real)
    /-- The imaginary part-/
    (im: Real)

namespace MyComplex


section zero

instance hasZero: Zero MyComplex where
  zero := âŸ¨0,0âŸ©

/-- Real part of `0`-/
@[simp]
theorem zero_re : (0 : MyComplex).re = 0 := rfl

/-- Imaginary part of `0`-/
@[simp]
theorem zero_im : (0 : MyComplex).im = 0 := rfl

/-- The norm squared of a complex number is zero iff the complex number is zero -/
-- We need this later on for `mul_add`
theorem normSq_eq_zero_iff {x: MyComplex}: x = 0 â†” (x.re^2 + x.im^2) = 0 := by
  constructor <;> intro h
  . simp only [MyComplex.ext_iff, zero_re, zero_im] at h
    rw [h.left, h.right]
    simp only [ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, add_zero]
  . have h2: x.re^2 â‰¥ 0 := sq_nonneg x.re
    have h3: x.im^2 â‰¥ 0 := sq_nonneg x.im
    have h4: x.re^2 = 0 âˆ§ x.im^2 = 0 := (add_eq_zero_iff' h2 h3).mp h
    have h5: x.re = 0 := sq_eq_zero_iff.mp h4.left
    have h6: x.im = 0 := sq_eq_zero_iff.mp h4.right
    exact MyComplex.ext x 0 h5 h6


end zero
section one

instance hasOne: One MyComplex where
  one := âŸ¨1,0âŸ©

/- Real part of one-/
@[simp]
theorem one_re: (1: MyComplex).re = 1 := rfl

/- Imaginary part of one-/
@[simp]
theorem one_im: (1: MyComplex).im = 0 := rfl

end one

section add
instance hasAdd: Add MyComplex where
  add (x y: MyComplex) := âŸ¨x.re + y.re, x.im + y.imâŸ©

/- Real part of `x + y` -/
@[simp]
theorem add_re (x y: MyComplex): (x + y).re = x.re + y.re := rfl
/- Imaginary part of `x + y` -/
@[simp]
theorem add_im (x y: MyComplex): (x + y).im = x.im + y.im := rfl

/- Zero is the additive identity: ğœ† + 0 = ğœ† -/
nonrec theorem add_zero: âˆ€x: MyComplex, x + 0 = x := by
  intro x ; ext
  . simp only [add_re, zero_re, add_zero]
  . simp only [add_im, zero_im, add_zero]

/- Addition is commmutative: ğ›¼ + ğ›½ = ğ›½ + ğ›¼ -/
theorem add_comm (x y: MyComplex): x + y = y + x := by
  simp [MyComplex.ext_iff, add_re, add_im] ; ring_nf ; simp only [and_self]

/- Addition is associative: (ğ›¼ + ğ›½) + ğœ† = ğ›¼ + (ğ›½ + ğœ†)-/
theorem add_assoc (x y: MyComplex): x + (y + z) = (x + y) + z := by
  ext<;> simp <;> ring

end add

section neg

instance hasNeg: Neg MyComplex where
  neg (x: MyComplex) := âŸ¨-x.re, -x.imâŸ©

/- Real part of `-z`-/
@[simp]
theorem neg_re (z: MyComplex): (-z).re = -(z.re) := rfl

/- Imaginary part of `-z`-/
@[simp]
theorem neg_im (z: MyComplex): (-z).im = -z.im := rfl

/- Existence of additive inverse: For every ğ›¼ âˆˆ ğ‚, there exists a unique ğ›½ âˆˆ ğ‚ such that ğ›¼ + ğ›½ = 0-/
theorem exists_add_inv: âˆ€x : MyComplex, âˆƒy, x + y = 0 := fun x =>
  âŸ¨-x, by
    ext
    . rw [add_re, neg_re, zero_re, add_neg_self]
    . rw [add_im, neg_im, zero_im, add_neg_self]âŸ©

end neg

section mul

instance: Mul MyComplex where
  mul (x y: MyComplex) := âŸ¨x.re * y.re - x.im * y.im, x.re * y.im + x.im * y.reâŸ©

/- Real part of `z * w`-/
@[simp]
theorem mul_re (z w: MyComplex): (z * w).re = z.re * w.re - z.im * w.im := rfl

/- Imaginary part of `z * w`-/
@[simp]
theorem mul_im (z w: MyComplex): (z * w).im = z.re * w.im + z.im * w.re := rfl


/- Zero-product: `x * 0 = 0`-/
@[simp]
nonrec theorem mul_zero (x: MyComplex): x * 0 = 0 := by
  ext
  . rw [mul_re, zero_re, zero_im, mul_zero, mul_zero, sub_zero]
  . rw [mul_im, zero_im, zero_re, mul_zero, mul_zero, _root_.add_zero]

/- Multiplicative identity: `ğœ†1 = ğœ†` for all `ğœ† âˆˆ ğ‚`-/
@[simp]
theorem mul_one (x: MyComplex): x * 1 = x := by
  simp only [MyComplex.ext_iff, one_re, one_im, mul_re, mul_im]; ring_nf; simp only [and_self]

/- Multiplication is commutative: `ğ›¼ğ›½ = ğ›½ğ›¼` for all `ğ›¼, ğ›½ âˆˆ ğ‚`-/
theorem mul_comm (x y: MyComplex): x * y = y * x := by
   simp only [MyComplex.ext_iff, mul_re, mul_im]; ring_nf; simp only [and_true]

/- Multiplication is associative: `(ğ›¼ğ›½)ğœ† = ğ›¼(ğ›½ğœ†) for all ğ›¼, ğ›½, ğœ† âˆˆ ğ‚`-/
theorem mul_assoc (x y z: MyComplex): x * (y * z) = (x * y) * z := by
   simp only [MyComplex.ext_iff, mul_re, mul_im]; ring_nf; simp only [and_true]

end mul

section inv

noncomputable instance hasInv: Inv MyComplex where
  inv (x : MyComplex) := âŸ¨x.re / (x.re^2 + x.im^2), -x.im/(x.re^2 + x.im^2)âŸ©

/- Real part of `zâ»Â¹`-/
@[simp]
theorem inv_re (z : MyComplex): zâ»Â¹.re = z.re / (z.re^2 + z.im^2) := rfl

/- Imaginary part of `zâ»Â¹`-/
@[simp]
theorem inv_im (x: MyComplex): xâ»Â¹.im = -x.im/(x.re^2 + x.im^2) := rfl

/-Existence of multiplicative inverse: For every ğ›¼ âˆˆ ğ‚ with ğ›¼ â‰  0, there exists a unique ğ›½ âˆˆ ğ‚ such that ğ›¼ğ›½ = 1-/
theorem exists_mul_inv: âˆ€ x : MyComplex, x â‰  0 â†’  âˆƒy, x * y = 1 := by
  intro x h
  use xâ»Â¹
  unfold MyComplex.hasInv
  ext
  . simp only [mul_re, inv_re, inv_im, one_re, mul_add]
    field_simp
    rw [â†sq, â† sq, â†sub_div, sub_neg_eq_add, div_eq_mul_inv, mul_inv_cancel]
    by_contra h2
    have h3 := normSq_eq_zero_iff.mpr h2
    contradiction
  . simp only [mul_im, inv_re, inv_im, one_im, mul_add]
    field_simp
    rw [_root_.mul_comm x.re]; field_simp

end inv

/- Distributive property: ğœ†(ğ›¼ + ğ›½) = ğœ†ğ›¼ + ğœ†ğ›½ for all ğœ†, ğ›¼, ğ›½ âˆˆ ğ‚.-/
theorem mul_add {x y z: MyComplex}: x * (y + z) = x*y + x*z := by
  simp only [MyComplex.ext_iff, mul_re, add_re, add_im, mul_im]
  constructor <;> ring_nf

end MyComplex
